plugins {
    alias(libs.plugins.android.test)
    alias(libs.plugins.kotlin.android)
}

android {
    namespace 'app.tivi.benchmark'

    compileSdk libs.versions.compileSdk.get() as int

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdk libs.versions.minSdk.get() as int
        targetSdk libs.versions.targetSdk.get() as int

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        benchmark {
            debuggable = true
            signingConfig = debug.signingConfig
            matchingFallbacks = ["release"]
            minifyEnabled = true
            proguardFiles 'benchmark-rules.pro'
        }
    }

    targetProjectPath = ':app'
    experimentalProperties["android.experimental.self-instrumenting"] = true
}

dependencies {
    implementation libs.androidx.test.junit
    implementation 'androidx.benchmark:benchmark-macro-junit4:1.1.0'
    implementation "androidx.test.uiautomator:uiautomator:2.2.0"
    implementation libs.androidx.test.junit
    implementation libs.kotlin.coroutines.android
}

androidComponents {
    beforeVariants(selector().all()) {
        enabled = buildType == "benchmark"
    }
}
